vocabulary <http://example.com/project/vocabulary/UTP_Data#> as UTP_Data {

	extends <http://uaontologies.com/UA_Foundation/UA_Foundation#> as foundation
	extends <http://uaontologies.com/UA_Core/UA_Information#> as info
	extends <http://uaontologies.com/UA_Core/UA_Agent#> as agent
	extends <http://example.com/project/vocabulary/UTP_Configuration#> as utp_config

	// Concepts	
	concept DataPool < info:InformationArtifact, foundation:Container [
		restricts some foundation:hasGenericDependent to info:DescriptiveInformationEntity
		restricts all foundation:contains to DataItem
	]

	concept DataItem < info:InformationArtifact [
    	restricts some foundation:hasGenericDependent to info:DescriptiveInformationEntity
    	restricts all foundation:isContainedIn to DataPool
	]

	concept DataPartition < foundation:Role [
		restricts some foundation:genericallyDependentOn to DataPool
	]

	concept DataSpecification < info:PrescriptiveInformationEntity [
		restricts all info:prescribes to DataPool
		restricts some info:prescribes to DataPool
		restricts some informs to DataProvider
		restricts some foundation:contains to Constraint
		restricts some foundation:contains to DataType
	]

	concept Constraint < info:PrescriptiveInformationEntity [
		restricts all info:prescribes to DataItem
		restricts some info:prescribes to DataItem
		restricts all foundation:isContainedIn to DataSpecification
	]

	concept DataType < info:PrescriptiveInformationEntity [
		restricts all info:prescribes to DataItem
		restricts some info:prescribes to DataItem
		restricts all foundation:isContainedIn to DataSpecification
	]

	// Replaced reads with hasInput
	concept DataProvider < utp_config:TestComponent, agent:Producer, agent:Consumer [
		restricts all agent:hasInput to DataPool
		restricts all agent:hasOutput to DataPool
		restricts some agent:hasOutput to DataPool
		
	]

	// Relations
	relation entity ConformsTo [
		from DataItem
		to DataSpecification
		forward conformsTo
		reverse isConformedBy
		asymmetric
		irreflexive
	]

	relation entity Informs [
		from DataSpecification
		to DataProvider
		forward informs
		reverse isInformedBy
		asymmetric
		irreflexive
	]
}
